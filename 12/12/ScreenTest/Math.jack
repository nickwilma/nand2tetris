// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array toTwo;

    /** Initializes the library. */
    function void init() {
        var int i, val;

        let toTwo = Array.new(16);
        let i = 0;
        let val = 1;

        while (i < 16) {
            let toTwo[i] = val;
            let val = val + val;
            
            let i = i + 1;
        }

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if(x < 0) {
            return -x;
        }
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, i, a, b, shift;

        let a = Math.abs(x);
        let b = Math.abs(y);
        let shift = a;

        while (i < 16) {
            if (Math.isBit(b, i)) {
                let sum = sum + shift;
            }

            let shift = shift + shift;
            let i = i + 1;
        }

        if((x > 0 & y > 0) | (x < 0 & y < 0)) {
            return sum;
        }

        return -sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int a, b, temp, sum;

        let a = Math.abs(x);
        let b = Math.abs(y);

        if(b > a) {
            return 0;
        }

        let temp = Math.divide(a, b + b);
        let sum = temp + temp;
        
        if(~((a - (2 * temp * b)) < b)) {
            let sum = temp + temp + 1;
        }

        if((x > 0 & y > 0) | (x < 0 & y < 0)) {
            return sum;
        }

        return -sum;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int a, b, q, temp;

        let a = 0;
        let b = 7;

        while(~(b < 0)) {
            let temp = a + Math.twoTo(b);
            let q = temp * temp;

            if(q > 0){
                if(~(q > x)) {
                    let a = temp;
                }
            }
            
            let b = b - 1;
        }

        return a;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if(b > a) {
            return b;
        }
        return a;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if(b < a) {
            return b;
        }
        return a;
    }

    /* Helper function */
    function int twoTo(int x) {
        return toTwo[x];
    }

    /* Helper function */
    function boolean isBit(int a, int i) {
      return (Math.twoTo(i) & a) > 0;
    }
}
