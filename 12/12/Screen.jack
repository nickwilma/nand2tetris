// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static Array display;
    static boolean color;

    /** Initializes the Screen. */
    function void init() {
        let display = 16384;
        let color = -1;

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i;
        let i = 0;
        
        while(i < (24575 - 16384)) {
            let display[i] = 0;
            let i = i + 1;
        }

        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;

        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, value;
        
        let address = (y * 32) + (x / 16);
        let value = display[address];

        let value = Math.twoTo(x & 15);
    
        if(color) {
            let display[address] = display[address] | Math.twoTo(x & 15);
            return;
        }

        let display[address] = display[address] & (~ Math.twoTo(x & 15));

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int a, b, dx, dy, temp;

		let a = 0;
		let b = 0;

        // reverse
        if(x1 > x2) {
            let temp = x1;
            let x1 = x2;
            let x2 = temp;

            let temp = y1;
            let y1 = y2;
            let y2 = temp;
        }

        let dx = x2 - x1;
        let dy = y2 - y1;

		if(dx = 0) {
			do Screen.drawLineVertical(x1, y1, x2, y2, a, b, dx, dy);
            return;
		}
        
        if(dy = 0) {
			do Screen.drawLineHorizontal(x1, y1, x2, y2, a, b, dx, dy);
            return;
		}

        do Screen.drawLineDiagonal(x1, y1, x2, y2, a, b, dx, dy);

        return;
    }

    /* Helper function */
    function void drawLineVertical(int x1, int y1, int x2, int y2, int a, int b, int dx, int dy) {

        do Screen.drawPixel(x1, y1);

        while(~(b = dy)) {
            if(dy < 0) {
                let b = b - 1;
            } else {
                let b = b + 1;
            }
            
            do Screen.drawPixel(x1, y1 + b);
        }

        return;
    }

    /* Helper function */
    function void drawLineHorizontal(int x1, int y1, int x2, int y2, int a, int b, int dx, int dy) {

        do Screen.drawPixel(x1, y1);

        while(~(a = dx)) {
            if(dx < 0) {
                let a = a - 1;
            } else {
                let a = a + 1;
            }

            do Screen.drawPixel(x1 + a, y1);
        }
        return;
    }

    /* Helper function */
    function void drawLineDiagonal(int x1, int y1, int x2, int y2, int a, int b, int dx, int dy) {
        var int y, temp;

        let y = 1;
        let temp = 0;
        
        if(dy < 0) {
            let y = -1;
        }

        while(Screen.isFurtherPixel(y, dx, dy, a, b)) {
            do Screen.drawPixel(x1 + a, y1 + b);
            
            if(temp < 0) {
                let temp = (dy * y) + temp;
                let a = a + 1;
            }
            else {
                let temp = temp - dx;
                let b = b + y;
            }
        }

        return;
    }

    /* Helper function */
    function boolean isFurtherPixel(int y, int dx, int dy, int a, int b) {
        var boolean aLower, above0, above1;

        let aLower = ~(a > dx);
        let above0 = (y = 1) & ~(b > dy);
        let above1 = (y = -1) & ~(b < dy);

        return aLower & (above0 | above1);
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
		do Screen.drawLine(x1, y1, x2, y1);

		while(~(y1=y2)){
			let y1 = y1 + 1;
			do Screen.drawLine(x1, y1, x2, y1);
		}

		return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int tempX, tempY;
		
		let tempY = -r;
		
		while(~(tempY > r))
		{
			let tempX = Math.sqrt((r * r) - (tempY * tempY));

			do Screen.drawLine(x - tempX, y + tempY, x + tempX, y + tempY);
			let tempY = tempY + 1;
		}
		return;
    }
}
