// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Math.init();
        do Memory.init();
        do Output.init();
        do Keyboard.init();
        do Screen.init();
        do Main.main();

        do Sys.halt();

        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(-1) {}

        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i;
        let duration = Math.abs(duration);

        while(duration > 0) {
            let i = 0;

            while(i < 100) {
                let i = i + 1;
            }

            let duration = duration - 1;
        }

        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        var String str0, str1;
        let str0 = "ERR<";
        let str1 = ">";

        do Output.printString(str0);
        do Output.printInt(errorCode);
        do Output.printString(str1);

        do str0.dispose();
        do str1.dispose();

        do Sys.halt();

        return;
    }
}
